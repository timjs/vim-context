" Vim syntax file
" Language:         ConTeXt typesetting engine
" Maintainer:       Tim Steenvoorden <steenvoo@science.ru.nl>
" Latest Revision:  2012-05-29
" TODO:
"   * minus in digits.

" Initialize Syntaxfile: {{{1
" ======================

if exists('b:current_syntax')
  finish
endif

let s:cpo_save = &cpo
set cpo&vim

scriptencoding utf-8

setlocal isk-=_

syn spell toplevel

" Syntax Definitions:
" ===================

" Commands: {{{1
" ---------

" We just match \...alphabetic... Sometimes _, @ and ! are used inside commands, but this is only supported inside \unprotect'ed environments.
syn match   contextCommand    display '\\\a\+'
syn match   contextCommand            '\\\%(\a\|_\|@\|!\|?\)\+' contained containedin=contextUnprotect

syn region  contextUnprotect  transparent matchgroup=contextDelimiter start='\\unprotect' end='\\protect'

" Some commands are special statements...
syn match   contextBlock      display '\\\%(start\|stop\)\a*'

" ...or section heads.
syn match   contextHead       display '\\\%(start\|stop\)\?part\>'
syn match   contextHead       display '\\\%(start\|stop\)\?\%(chapter\|title\)\>'
syn match   contextHead       display '\\\%(start\|stop\)\?\%(sub\)*\%(section\|subject\)\>'

" The mistake I make most often.
syn match   contextStructureError     display '\\\%(start\)\?\%(component\|product\|project\|environment\)\>'
syn match   contextStructureError     display '\\input\>'

" And these define the document structure.
syn match   contextStructure  display '^\s*\\\%(start\)\?\%(component\|product\|project\|environment\)\s\+\S\+$'
syn match   contextStructure  display '^\s*\\stop\%(component\|product\|project\|environment\)$'
syn match   contextStructure  display '^\s*\\input\s\+\S\+$'
syn match   contextStructure  display '^\s*\\\%(start\|stop\)text$'

" Definitions And Setups: {{{1
" -----------------------

syn match   contextDefine     display '\\\%([egx]\?def\|let\)\>'
syn match   contextDefine     display '\\\%(re\)\?define\>'
syn match   contextDefine     display '\\\%(define\|\%(re\)\?set\|get\|let\|new\)\a\+'
syn match   contextDefine     display '\\\%(start\|stop\)texdefinition\>'

syn match   contextSetup      display '\\\%(setup\|use\|enable\|disable\|prevent\|show\)\a\+'
syn match   contextSetup      display '\\\%(start\|stop\)\?setups\>'

syn match   contextCondition  display '\\\%(if\a\+\|else\|fi\)\>'
syn match   contextCondition  display '\\doif\a*' 
syn match   contextCondition  display '\\\%(start\|stop\)\%(not\)\?\%(all\)\?mode\%(set\)\?\>'

syn match   contextLoop       display '\\do\%(\%(stepwise\)\?recurse\|loop\)\>'
syn match   contextLoop       display '\\\%(recurselevel\|exitloop\)\>'


" Groups And Arguments: {{{1
" -------------------

" The purpose of this code is only to highlight matching braces and parenthesis. Just as the folds defined below they are transparent. Math concealment doesn't work inside transparent regions, so we've to define these groups once again below.
syn region  contextGroup      transparent matchgroup=contextDelimiter start='{'  end='}'
syn region  contextGroup      transparent matchgroup=contextDelimiter start='('  end=')'

" To get rid of nasty spell errors for options, we don't allow spell check inside argument brackets. Here we can't use the transparent option.
" We don't define this as a 'nextgroup' after commands to save us some work.
syn region  contextArgument               matchgroup=contextDelimiter start='\[' end='\]' contains=TOP,@Spell,contextScriptError

" Ending delimiters that are not matched by the groups above (which have priority because the opening starts earlier), are matched as errors.
syn match   contextMismatch   display '[]})]'

" As a bonus, we can highlight some constants inside argument brackets.
syn match   contextLabel      display '\a\+:[0-9a-zA-Z_\-: ]\+'                                                               contained containedin=contextArgument
syn match   contextNumber     display '\<[+-]\?\%(\d\+\%(\.\d\+\)\?\|\.\d\+\)\>'                                              contained containedin=contextArgument
syn match   contextDimension  display '\<[+-]\?\%(\d\+\%(\.\d\+\)\?\|\.\d\+\)\%(p[tc]\|in\|bp\|cc]\|[cm]m\|dd\|sp\|e[mx]\)\>' contained containedin=contextArgument

syn keyword contextConstant   yes no on off start stop true false contained containedin=contextArgument

"TODO Maybe highlight more constants, probably do it this way.
"syn match   contextConstant   display '\<\%(yes\|no\|on\|off\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(fit\|broad\|fixed\|local\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(depth\|hanging\|high\|lohi\|low\|top\|middle\|bottom\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(serif\|regular\|roman\|sans\|support\|sansserif\|mono\|type\|teletype\|handwritten\|calligraphic\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(normal\|bold\|slanted\|boldslanted\|type\|cap\|small\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(small\|medium\|big\|nowhite\|back\|white\|disable\|force\|reset\|line\|halfline\|fixed\|flexible\|none\|samepage\)=\@!\>' contained containedin=contextArgument
"syn match   contextConstant   display '\<\%(left\|right\|here\|top\|bottom\|inleft\|inright\|inmargin\|margin\|leftmargin\|rightmargin\|leftedge\|rightedge\|innermargin\|outermargin\|inneredge\|outeredge\|inner\|outer\|line\|high\|low\|fit\|page\|leftpage\|rightpage\|opposite\|always\|auto\|force\|tall\|reset\|line\|height\|depth\)=\@!\>' contained containedin=contextArgument

" Specials: {{{1
" ---------

syn match   contextEscaped    display '\\\W'
"syn match   contextEscaped    display '\\[%#~&$^_\{} \n]'
"syn match   contextEscaped    display '\\[`'"]'

syn match   contextSpecial    display '\\\%(par\|crlf\)\>'
syn match   contextSpecial    display '\\\@!\%(\~\|\^\|_\|-\{2,3}\)'
syn match   contextSpecial    display '\\$'
"syn match   contextSpecial    display '|[<>/]\?|'

" These are not allowed outside math mode, see math section.
syn match   contextScriptError        display '[_^]'

syn match   contextParameterError     display '#'
syn match   contextParameter  display '\\\@!#\+\d\+'

" Fonts And Styles: {{{1
" -----------------

syn match   contextFont       display '\\\%(rm\|ss\|tt\|hw\|cg\|mf\)\%(xx\|[xabcd]\)\?\>'

syn match   contextFont       display '\\\%(em\|tf\|b[fsi]\|s[cl]\|it\|os\)\%(xx\|[xabcd]\)\?\>'
syn match   contextFont       display '\\\%(vi\{1,3}\|ix\|xi\{0,2}\)\>'

syn match   contextStyle      display '\\\%(cap\|Cap\|CAP\|Caps\|nocap\)\>'
syn match   contextStyle      display '\\\%(word\|Word\|WORD\|Words\|WORDS\)\>'
syn match   contextStyle      display '\\\%(underbar\|over\%(bar\|strike\)\)s\?'
"FIXME What is this?
"syn match   contextStyle      display '\\\%(character\|Character\)s\?\>'

syn region  contextNormal      display matchgroup=contextDelimiter start='{\\tf\s'     end='}' contains=TOP
syn region  contextItalic      display matchgroup=contextDelimiter start='{\\it\s'     end='}' contains=TOP
syn region  contextSlanted     display matchgroup=contextDelimiter start='{\\sl\s'     end='}' contains=TOP
syn region  contextBold        display matchgroup=contextDelimiter start='{\\bf\s'     end='}' contains=TOP
syn region  contextBoldItalic  display matchgroup=contextDelimiter start='{\\bi\s'     end='}' contains=TOP
syn region  contextBoldItalic  display matchgroup=contextDelimiter start='{\\bf\\it\s' end='}' contains=TOP
syn region  contextBoldItalic  display matchgroup=contextDelimiter start='{\\it\\bf\s' end='}' contains=TOP
syn region  contextBoldSlanted display matchgroup=contextDelimiter start='{\\bs\s'     end='}' contains=TOP
syn region  contextBoldSlanted display matchgroup=contextDelimiter start='{\\bf\\sl\s' end='}' contains=TOP
syn region  contextBoldSlanted display matchgroup=contextDelimiter start='{\\sl\\bf\s' end='}' contains=TOP

syn region  contextAlert       display matchgroup=contextDelimiter start='\\alert{'           end='}'                 contains=TOP

syn region  contextOuterEmph   display matchgroup=contextDelimiter start='{\\em\s'            end='}'                 contains=TOP,contextOuterEmph
syn region  contextOuterEmph   display matchgroup=contextDelimiter start='\\emph{'            end='}'                 contains=TOP,contextOuterEmph
syn region  contextOuterEmph           matchgroup=contextBlock     start='\\startemphasize\>' end='\\stopemphasize\>' contains=TOP,contextOuterEmph

syn region  contextInnerEmph   display matchgroup=contextDelimiter start='{\\em\s'            end='}'                 contains=TOP,contextInnerEmph contained containedin=contextOuterEmph,context.*\(Italic\|Slanted\)
syn region  contextInnerEmph   display matchgroup=contextDelimiter start='\\emph{'            end='}'                 contains=TOP,contextInnerEmph contained containedin=contextOuterEmph,context.*\(Italic\|Slanted\)
syn region  contextInnerEmph           matchgroup=contextBlock     start='\\startemphasize\>' end='\\stopemphasize\>' contains=TOP,contextInnerEmph contained containedin=contextOuterEmph,context.*\(Italic\|Slanted\)

" Mathematics: {{{1
" ------------

syn region  contextMath       display matchgroup=contextDelimiter start='\$'                       end='\$'                 contains=TOP,@Spell,contextScriptError
syn region  contextMath       display matchgroup=contextDelimiter start='\\math\%(ematics\)\?{'    end='}'                  contains=TOP,@Spell,contextScriptError
syn region  contextMath       display matchgroup=contextDelimiter start='\\formula{'               end='}'                  contains=TOP,@Spell,contextScriptError
syn region  contextMath       display matchgroup=contextDelimiter start='\\chemical{'              end='}'                  contains=TOP,@Spell,contextScriptError
syn region  contextMath       display matchgroup=contextDelimiter start='\\molecule{'              end='}'                  contains=TOP,@Spell,contextScriptError

syn region  contextMath               matchgroup=contextDelimiter start='\$\$'                     end='\$\$'               contains=TOP,@Spell,contextScriptError
syn region  contextMath               matchgroup=contextBlock     start='\\start\z(\a*\)formula\>' end='\\stop\z1formula\>' contains=TOP,@Spell,contextScriptError

syn region  contextMathText   display matchgroup=contextDelimiter start='\\\%(inter\)\?text{'      end='}'                  contains=TOP contained containedin=contextMath

" Because math concealment doesn't work well with the transparent groups defined by contextGroup, we define them here once again.
syn region  contextMath    transparent matchgroup=contextDelimiter start='{'                        end='}'                 contained containedin=contextMath
syn region  contextMath    transparent matchgroup=contextDelimiter start='('                        end=')'                 contained containedin=contextMath
syn region  contextMath    transparent matchgroup=contextDelimiter start='\['                       end='\]'                contained containedin=contextMath

" Math Concealment: {{{1
" ~~~~~~~~~~~~~~~~~
"
" Many symbols by BjÃ¶rn Winckler, additions by Tim Steenvoorden.

if has('conceal') && &enc == 'utf-8'

  " Let user determine which classes of concealment will be supported:
  "   m = math symbols
  "   f = fractions
  "   s = spaces
  "   d = delimiters
  "   g = Greek
  "   l = Latin superscripts/subscripts
  "   n = numeric superscripts/subscripts
  "   b = blackboard, calligraphic and fraktur
  "   B = short blackboard
  "   a = accents
  "   S = sub- and superscripts inside braces
  " By default we conceal everything.
  if !exists('g:context_conceal')
   let s:context_conceal = 'mfsdglnbB'
  else
   let s:context_conceal = g:context_conceal
  endif

  if s:context_conceal =~ 'S'
    " We will define the unbraced variants separately for each symbol in the function ContextConcealScript.
    syn region contextSuperscript start='\^{' end='}' concealends contained containedin=contextMath contains=contextSuperscript,contextSubscript,contextSuperscripts,contextCommand
    syn region contextSubscript   start='_{'  end='}' concealends contained containedin=contextMath contains=contextSuperscript,contextSubscript,contextSubscripts,contextCommand
  endif

  fun! s:ContextConcealSymbol(pattern, replacement)
    exe 'syn match contextMathSymbol "'.a:pattern.'" contained containedin=contextMath conceal cchar='.a:replacement
  endfun

  fun! s:ContextRegisterAccent(name, accent)
    exe 'syn match   contextMathAccent'.a:name.' "}" contained conceal cchar='.a:accent
    exe 'hi def link contextMathAccent'.a:name.' contextMathAccent'
  endfun
  
  fun! s:ContextConcealAccent(name, pattern, replacement)
    exe 'syn match contextMathSymbol "\\'.a:name.'{'.a:pattern.'" nextgroup=contextMathAccent'.a:name.' contained containedin=contextMath conceal cchar='.a:replacement
  endfun

  fun! s:ContextConcealScript(pattern, superscript, subscript)
    " Scripts are only concealed if there is a suitable character in the Unicode tables.
    if a:superscript != ' '
      exe 'syn match contextSuperscript "\^'.a:pattern.'" contained containedin=contextMath conceal cchar='.a:superscript
      exe 'syn match contextSuperscripts  "'.a:pattern.'" contained                         conceal cchar='.a:superscript.' nextgroup=contextSuperscripts'
    endif
    if a:subscript != ' '
      exe 'syn match contextSubscript    "_'.a:pattern.'" contained containedin=contextMath conceal cchar='.a:subscript
      exe 'syn match contextSubscripts    "'.a:pattern.'" contained                         conceal cchar='.a:subscript.'   nextgroup=contextSubscripts'
    endif
  endfun

  " Math Symbols: {{{2
  let s:contextMathSymbols = [
    \ ['angle'              , 'âˆ '],
    \ ['approx'             , 'â‰ˆ'],
    \ ['ast'                , 'âˆ—'],
    \ ['asymp'              , 'â‰'],
    \ ['backepsilon'        , 'âˆ'],
    \ ['backsimeq'          , 'â‰ƒ'],
    \ ['backslash'          , '\'],
    \ ['barwedge'           , 'âŠ¼'],
    \ ['because'            , 'âˆµ'],
    \ ['between'            , 'â‰¬'],
    \ ['bigcap'             , 'âˆ©'],
    \ ['bigcup'             , 'âˆª'],
    \ ['bigodot'            , 'âŠ™'],
    \ ['bigoplus'           , 'âŠ•'],
    \ ['bigotimes'          , 'âŠ—'],
    \ ['bigsqcup'           , 'âŠ”'],
    \ ['bigtriangledown'    , 'âˆ‡'],
    \ ['bigvee'             , 'â‹'],
    \ ['bigwedge'           , 'â‹€'],
    \ ['blacksquare'        , 'âˆ'],
    \ ['booleans'           , 'ğ”¹'],
    \ ['bot'                , 'âŠ¥'],
    \ ['bowtie'             , 'â‹ˆ'],
    \ ['boxdot'             , 'âŠ¡'],
    \ ['boxminus'           , 'âŠŸ'],
    \ ['boxplus'            , 'âŠ'],
    \ ['boxtimes'           , 'âŠ '],
    \ ['bumpeq'             , 'â‰'],
    \ ['Bumpeq'             , 'â‰'],
    \ ['cap'                , 'âˆ©'],
    \ ['Cap'                , 'â‹’'],
    \ ['cdot'               , 'Â·'],
    \ ['cdots'              , 'â‹¯'],
    \ ['circ'               , 'âˆ˜'],
    \ ['circeq'             , 'â‰—'],
    \ ['circlearrowleft'    , 'â†º'],
    \ ['circlearrowright'   , 'â†»'],
    \ ['circledast'         , 'âŠ›'],
    \ ['circledcirc'        , 'âŠš'],
    \ ['complement'         , 'âˆ'],
    \ ['complexes'          , 'â„‚'],
    \ ['cong'               , 'â‰…'],
    \ ['coprod'             , 'âˆ'],
    \ ['cup'                , 'âˆª'],
    \ ['Cup'                , 'â‹“'],
    \ ['curlyeqprec'        , 'â‹'],
    \ ['curlyeqsucc'        , 'â‹Ÿ'],
    \ ['curlyvee'           , 'â‹'],
    \ ['curlywedge'         , 'â‹'],
    \ ['dashv'              , 'âŠ£'],
    \ ['diamond'            , 'â—‡'],
    \ ['div'                , 'Ã·'],
    \ ['doteq'              , 'â‰'],
    \ ['doteqdot'           , 'â‰‘'],
    \ ['dotplus'            , 'âˆ”'],
    \ ['dotsb'              , 'â‹¯'],
    \ ['dotsc'              , 'â€¦'],
    \ ['dots'               , 'â€¦'],
    \ ['dotsi'              , 'â‹¯'],
    \ ['dotso'              , 'â€¦'],
    \ ['doublebarwedge'     , 'â©'],
    \ ['downarrow'          , 'â†“'],
    \ ['Downarrow'          , 'â‡“'],
    \ ['emptyset'           , 'âˆ…'],
    \ ['eqcirc'             , 'â‰–'],
    \ ['eqsim'              , 'â‰‚'],
    \ ['eqslantgtr'         , 'âª–'],
    \ ['eqslantless'        , 'âª•'],
    \ ['equiv'              , 'â‰¡'],
    \ ['exists'             , 'âˆƒ'],
    \ ['fallingdotseq'      , 'â‰’'],
    \ ['forall'             , 'âˆ€'],
    \ ['ge'                 , 'â‰¥'],
    \ ['geq'                , 'â‰¥'],
    \ ['geqq'               , 'â‰§'],
    \ ['gets'               , 'â†'],
    \ ['gg'                 , 'â‰«'],
    \ ['gneqq'              , 'â‰©'],
    \ ['gtrdot'             , 'â‹—'],
    \ ['gtreqless'          , 'â‹›'],
    \ ['gtrless'            , 'â‰·'],
    \ ['gtrsim'             , 'â‰³'],
    \ ['hbar'               , 'Ä§'],
    \ ['hslash'             , 'â„'],
    \ ['hookleftarrow'      , 'â†©'],
    \ ['hookrightarrow'     , 'â†ª'],
    \ ['iiint'              , 'âˆ­'],
    \ ['iint'               , 'âˆ¬'],
    \ ['Im'                 , 'â„‘'],
    \ ['implies'            , 'â‡’'],
    \ ['in'                 , 'âˆˆ'],
    \ ['infty'              , 'âˆ'],
    \ ['int'                , 'âˆ«'],
    \ ['integers'           , 'â„¤'],
    \ ['land'               , 'âˆ§'],
    \ ['lceil'              , 'âŒˆ'],
    \ ['ldots'              , 'â€¦'],
    \ ['le'                 , 'â‰¤'],
    \ ['leadsto'            , 'â†'],
    \ ['leftarrow'          , 'â†'],
    \ ['Leftarrow'          , 'â‡'],
    \ ['leftarrowtail'      , 'â†¢'],
    \ ['leftrightarrow'     , 'â†”'],
    \ ['Leftrightarrow'     , 'â‡”'],
    \ ['leftrightsquigarrow', 'â†­'],
    \ ['leftthreetimes'     , 'â‹‹'],
    \ ['leq'                , 'â‰¤'],
    \ ['leqq'               , 'â‰¦'],
    \ ['lessdot'            , 'â‹–'],
    \ ['lesseqgtr'          , 'â‹š'],
    \ ['lesssim'            , 'â‰²'],
    \ ['lfloor'             , 'âŒŠ'],
    \ ['ll'                 , 'â‰ª'],
    \ ['lnot'               , 'Â¬'],
    \ ['longleftarrow'      , 'âŸµ'],
    \ ['Longleftarrow'      , 'âŸ¸'],
    \ ['longrightarrow'     , 'âŸ¶'],
    \ ['Longrightarrow'     , 'âŸ¹'],
    \ ['lor'                , 'âˆ¨'],
    \ ['lneqq'              , 'â‰¨'],
    \ ['ltimes'             , 'â‹‰'],
    \ ['mapsto'             , 'â†¦'],
    \ ['measuredangle'      , 'âˆ¡'],
    \ ['mid'                , 'âˆ£'],
    \ ['mp'                 , 'âˆ“'],
    \ ['nabla'              , 'âˆ‡'],
    \ ['naturals'           , 'â„•'],
    \ ['ncong'              , 'â‰‡'],
    \ ['nearrow'            , 'â†—'],
    \ ['ne'                 , 'â‰ '],
    \ ['neg'                , 'Â¬'],
    \ ['neq'                , 'â‰ '],
    \ ['nexists'            , 'âˆ„'],
    \ ['ngeq'               , 'â‰±'],
    \ ['ngeqq'              , 'â‰±'],
    \ ['ngtr'               , 'â‰¯'],
    \ ['ni'                 , 'âˆ‹'],
    \ ['nleftarrow'         , 'â†š'],
    \ ['nLeftarrow'         , 'â‡'],
    \ ['nLeftrightarrow'    , 'â‡'],
    \ ['nleq'               , 'â‰°'],
    \ ['nleqq'              , 'â‰°'],
    \ ['nless'              , 'â‰®'],
    \ ['nmid'               , 'âˆ¤'],
    \ ['notin'              , 'âˆ‰'],
    \ ['nprec'              , 'âŠ€'],
    \ ['nrightarrow'        , 'â†›'],
    \ ['nRightarrow'        , 'â‡'],
    \ ['nsim'               , 'â‰'],
    \ ['nsucc'              , 'âŠ'],
    \ ['ntriangleleft'      , 'â‹ª'],
    \ ['ntrianglelefteq'    , 'â‹¬'],
    \ ['ntriangleright'     , 'â‹«'],
    \ ['ntrianglerighteq'   , 'â‹­'],
    \ ['nvdash'             , 'âŠ¬'],
    \ ['nvDash'             , 'âŠ­'],
    \ ['nVdash'             , 'âŠ®'],
    \ ['nwarrow'            , 'â†–'],
    \ ['odot'               , 'âŠ™'],
    \ ['oint'               , 'âˆ®'],
    \ ['ominus'             , 'âŠ–'],
    \ ['oplus'              , 'âŠ•'],
    \ ['oslash'             , 'âŠ˜'],
    \ ['otimes'             , 'âŠ—'],
    \ ['owns'               , 'âˆ‹'],
    \ ['partial'            , 'âˆ‚'],
    \ ['perp'               , 'âŠ¥'],
    \ ['pitchfork'          , 'â‹”'],
    \ ['pm'                 , 'Â±'],
    \ ['precapprox'         , 'âª·'],
    \ ['prec'               , 'â‰º'],
    \ ['preccurlyeq'        , 'â‰¼'],
    \ ['preceq'             , 'âª¯'],
    \ ['precnapprox'        , 'âª¹'],
    \ ['precneqq'           , 'âªµ'],
    \ ['precsim'            , 'â‰¾'],
    \ ['prod'               , 'âˆ'],
    \ ['propto'             , 'âˆ'],
    \ ['rationals'          , 'â„š'],
    \ ['rceil'              , 'âŒ‰'],
    \ ['Re'                 , 'â„œ'],
    \ ['reals'              , 'â„'],
    \ ['rfloor'             , 'âŒ‹'],
    \ ['rightarrow'         , 'â†’'],
    \ ['Rightarrow'         , 'â‡’'],
    \ ['rightarrowtail'     , 'â†£'],
    \ ['rightsquigarrow'    , 'â†'],
    \ ['rightthreetimes'    , 'â‹Œ'],
    \ ['risingdotseq'       , 'â‰“'],
    \ ['rtimes'             , 'â‹Š'],
    \ ['searrow'            , 'â†˜'],
    \ ['setminus'           , 'âˆ–'],
    \ ['sim'                , 'âˆ¼'],
    \ ['sphericalangle'     , 'âˆ¢'],
    \ ['sqcap'              , 'âŠ“'],
    \ ['sqcup'              , 'âŠ”'],
    \ ['sqsubset'           , 'âŠ'],
    \ ['sqsubseteq'         , 'âŠ‘'],
    \ ['sqsupset'           , 'âŠ'],
    \ ['sqsupseteq'         , 'âŠ’'],
    \ ['star'               , 'â‹†'],
    \ ['subset'             , 'âŠ‚'],
    \ ['Subset'             , 'â‹'],
    \ ['subseteq'           , 'âŠ†'],
    \ ['subseteqq'          , 'â«…'],
    \ ['subsetneq'          , 'âŠŠ'],
    \ ['subsetneqq'         , 'â«‹'],
    \ ['succapprox'         , 'âª¸'],
    \ ['succ'               , 'â‰»'],
    \ ['succcurlyeq'        , 'â‰½'],
    \ ['succeq'             , 'âª°'],
    \ ['succnapprox'        , 'âªº'],
    \ ['succneqq'           , 'âª¶'],
    \ ['succsim'            , 'â‰¿'],
    \ ['sum'                , 'âˆ‘'],
    \ ['Supset'             , 'â‹‘'],
    \ ['supseteq'           , 'âŠ‡'],
    \ ['supseteqq'          , 'â«†'],
    \ ['supsetneq'          , 'âŠ‹'],
    \ ['supsetneqq'         , 'â«Œ'],
    \ ['surd'               , 'âˆš'],
    \ ['swarrow'            , 'â†™'],
    \ ['therefore'          , 'âˆ´'],
    \ ['times'              , 'Ã—'],
    \ ['to'                 , 'â†’'],
    \ ['top'                , 'âŠ¤'],
    \ ['total'              , 'd'],
    \ ['triangleleft'       , 'âŠ²'],
    \ ['trianglelefteq'     , 'âŠ´'],
    \ ['triangleq'          , 'â‰œ'],
    \ ['triangleright'      , 'âŠ³'],
    \ ['trianglerighteq'    , 'âŠµ'],
    \ ['twoheadleftarrow'   , 'â†'],
    \ ['twoheadrightarrow'  , 'â† '],
    \ ['uparrow'            , 'â†‘'],
    \ ['Uparrow'            , 'â‡‘'],
    \ ['updownarrow'        , 'â†•'],
    \ ['Updownarrow'        , 'â‡•'],
    \ ['varnothing'         , 'âˆ…'],
    \ ['vartriangle'        , 'âˆ†'],
    \ ['vdash'              , 'âŠ¢'],
    \ ['vDash'              , 'âŠ¨'],
    \ ['Vdash'              , 'âŠ©'],
    \ ['vdots'              , 'â‹®'],
    \ ['veebar'             , 'âŠ»'],
    \ ['vee'                , 'âˆ¨'],
    \ ['Vvdash'             , 'âŠª'],
    \ ['wedge'              , 'âˆ§'],
    \ ['wr'                 , 'â‰€']]

  if s:context_conceal =~ 'm'
    for symbol in s:contextMathSymbols
      call s:ContextConcealSymbol('\\'.symbol[0].'\>', symbol[1])
    endfor
  endif
  
  " Fractions: {{{2
  let s:contextFractionSymbols = [
    \ ['half'         , 'frac12', 'Â½'],
    \ ['third'        , 'frac13', 'â…“'],
    \ ['thwothirds'   , 'frac23', 'â…”'],
    \ ['quarter'      , 'frac14', 'Â¼'],
    \ ['threequarters', 'frac34', 'Â¾'],
    \ ['fifth'        , 'frac15', 'â…•'],
    \ ['twofifths'    , 'frac25', 'â…–'],
    \ ['threefifths'  , 'frac35', 'â…—'],
    \ ['fourfifths'   , 'frac45', 'â…˜'],
    \ ['sixth'        , 'frac16', 'â…™'],
    \ ['fifesixths'   , 'frac56', 'â…š'],
    \ ['eighth'       , 'frac18', 'â…›'],
    \ ['threeeighths' , 'frac38', 'â…œ'],
    \ ['fifeeighths'  , 'frac58', 'â…'],
    \ ['seveneighths' , 'frac78', 'â…']]

  if s:context_conceal =~ 'f'
    for symbol in s:contextFractionSymbols
      call s:ContextConcealSymbol('\\'.symbol[0].'\>', symbol[2])
      call s:ContextConcealSymbol('\\'.symbol[1]     , symbol[2])
    endfor
  endif

  " Spaces: {{{2
  if s:context_conceal =~ 's'
    call s:ContextConcealSymbol('\\[;:,!]'      , 'â£')
    call s:ContextConcealSymbol('\\\%(q\)\?quad', 'â£')
  endif

  " Delimiters: {{{2
  let s:contextDelimiterSymbols = [
    \ ['\.'  , 'Â¦'],
    \ ['('   , '('],
    \ [')'   , ')'],
    \ ['\['  , '['],
    \ ['\]'  , ']'],
    \ ['<'   , 'âŸ¨'],
    \ ['>'   , 'âŸ©'],
    \ ['|'   , '|'],
    \ ['/'   , '/']]
  let s:contextDelimiterCommands = [
    \ ['lgroup'     , '('],
    \ ['rgroup'     , ')'],
    \ ['{'          , '{'],
    \ ['}'          , '}'],
    \ ['lbrace'     , '{'],
    \ ['rbrace'     , '}'],
    \ ['lbracket'   , '['],
    \ ['rbracket'   , ']'],
    \ ['lBracket'   , 'âŸ¦'],
    \ ['rBracket'   , 'âŸ§'],
    \ ['\['         , 'âŸ¦'],
    \ ['\]'         , 'âŸ§'],
    \ ['langle'     , 'âŸ¨'],
    \ ['rangle'     , 'âŸ©'],
    \ ['lAngle'     , 'âŸª'],
    \ ['rAngle'     , 'âŸ«'],
    \ ['<'          , 'âŸª'],
    \ ['>'          , 'âŸ«'],
    \ ['vert'       , '|'],
    \ ['lvert'      , '|'],
    \ ['rvert'      , '|'],
    \ ['Vert'       , 'â€–'],
    \ ['lVert'      , 'â€–'],
    \ ['rVert'      , 'â€–'],
    \ ['|'          , 'â€–'],
    \ ['backslash'  , '\'],
    \ ['lfloor'     , 'âŒŠ'],
    \ ['rfloor'     , 'âŒ‹'],
    \ ['lceil'      , 'âŒˆ'],
    \ ['rceil'      , 'âŒ‰'],
    \ ['uparrow'    , 'â†‘'],
    \ ['Uparrow'    , 'â‡‘'],
    \ ['downarrow'  , 'â†“'],
    \ ['Downarrow'  , 'â‡“'],
    \ ['updownarrow', 'â†•'],
    \ ['Updownarrow', 'â‡•'],
    \ ['llcorner'   , 'âŒ'],
    \ ['lrcorner'   , 'âŒŸ'],
    \ ['ulcorner'   , 'âŒœ'],
    \ ['urconrner'  , 'âŒ'],
    \ ['lmoustache' , 'â '],
    \ ['rmoustache' , 'â']]

  if s:context_conceal =~ 'd'
    "call s:ContextConcealSymbol('\\{', '{')
    "call s:ContextConcealSymbol('\\}', '}')
    for symbol in s:contextDelimiterSymbols
      call s:ContextConcealSymbol( '\\left'.symbol[0], symbol[1])
      call s:ContextConcealSymbol('\\right'.symbol[0], symbol[1])
      call s:ContextConcealSymbol(  '\\big'.symbol[0], symbol[1])
      call s:ContextConcealSymbol(  '\\Big'.symbol[0], symbol[1])
      call s:ContextConcealSymbol( '\\bigg'.symbol[0], symbol[1])
      call s:ContextConcealSymbol( '\\Bigg'.symbol[0], symbol[1])
    endfor
    for symbol in s:contextDelimiterCommands
      call s:ContextConcealSymbol(       '\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol( '\\left\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol('\\right\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol(  '\\big\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol(  '\\Big\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol( '\\bigg\\'.symbol[0].'\>', symbol[1])
      call s:ContextConcealSymbol( '\\Bigg\\'.symbol[0].'\>', symbol[1])
    endfor
  endif

  " Greek: {{{2
  let s:contextGreekSymbols = [
    \ ['alpha'     , 'Î±', ' ', ' '],
    \ ['beta'      , 'Î²', ' ', 'áµ¦'],
    \ ['gamma'     , 'Î³', ' ', 'áµ§'],
    \ ['delta'     , 'Î´', ' ', ' '],
    \ ['epsilon'   , 'Ïµ', ' ', ' '],
    \ ['varepsilon', 'Îµ', ' ', ' '],
    \ ['zeta'      , 'Î¶', ' ', ' '],
    \ ['eta'       , 'Î·', ' ', ' '],
    \ ['theta'     , 'Î¸', ' ', ' '],
    \ ['vartheta'  , 'Ï‘', ' ', ' '],
    \ ['kappa'     , 'Îº', ' ', ' '],
    \ ['lambda'    , 'Î»', ' ', ' '],
    \ ['mu'        , 'Î¼', ' ', ' '],
    \ ['nu'        , 'Î½', ' ', ' '],
    \ ['xi'        , 'Î¾', ' ', ' '],
    \ ['pi'        , 'Ï€', ' ', ' '],
    \ ['varpi'     , 'Ï–', ' ', ' '],
    \ ['rho'       , 'Ï', ' ', ' '],
    \ ['varrho'    , 'Ï±', ' ', 'áµ¨'],
    \ ['sigma'     , 'Ïƒ', ' ', ' '],
    \ ['varsigma'  , 'Ï‚', ' ', ' '],
    \ ['tau'       , 'Ï„', ' ', ' '],
    \ ['upsilon'   , 'Ï…', ' ', ' '],
    \ ['phi'       , 'Ï•', ' ', ' '],
    \ ['varphi'    , 'Ï†', ' ', 'áµ©'],
    \ ['chi'       , 'Ï‡', ' ', 'áµª'],
    \ ['psi'       , 'Ïˆ', ' ', ' '],
    \ ['omega'     , 'Ï‰', ' ', ' '],
    \ ['Gamma'     , 'Î“', ' ', ' '],
    \ ['Delta'     , 'Î”', ' ', ' '],
    \ ['Theta'     , 'Î˜', ' ', ' '],
    \ ['Lambda'    , 'Î›', ' ', ' '],
    \ ['Xi'        , 'Î§', ' ', ' '],
    \ ['Pi'        , 'Î ', ' ', ' '],
    \ ['Sigma'     , 'Î£', ' ', ' '],
    \ ['Upsilon'   , 'Î¥', ' ', ' '],
    \ ['Phi'       , 'Î¦', ' ', ' '],
    \ ['Psi'       , 'Î¨', ' ', ' '],
    \ ['Omega'     , 'Î©', ' ', ' ']]

  if s:context_conceal =~ 'g'
    for symbol in s:contextGreekSymbols
      call s:ContextConcealSymbol('\\'.symbol[0].'\>', symbol[1])
      "call s:ContextConcealScript('\\'.symbol[0].'\>', symbol[2], symbol[3])
    endfor
  endif

  " Latin: {{{2
  let s:contextLatinSymbols = [
    \ ['a', 'áµƒ', 'â‚', 'ğ•’', 'ğ’¶', 'ğ“ª', 'ğ”', 'ğ–†'],
    \ ['b', 'áµ‡', ' ', 'ğ•“', 'ğ’·', 'ğ“«', 'ğ”Ÿ', 'ğ–‡'],
    \ ['c', 'á¶œ', ' ', 'ğ•”', 'ğ’¸', 'ğ“¬', 'ğ” ', 'ğ–ˆ'],
    \ ['d', 'áµˆ', ' ', 'ğ••', 'ğ’¹', 'ğ“­', 'ğ”¡', 'ğ–‰'],
    \ ['e', 'áµ‰', 'â‚‘', 'ğ•–', 'â„¯', 'ğ“®', 'ğ”¢', 'ğ–Š'],
    \ ['f', 'á¶ ', ' ', 'ğ•—', 'ğ’»', 'ğ“¯', 'ğ”£', 'ğ–‹'],
    \ ['g', 'áµ', ' ', 'ğ•˜', 'â„Š', 'ğ“°', 'ğ”¤', 'ğ–Œ'],
    \ ['h', 'Ê°', ' ', 'ğ•™', 'ğ’½', 'ğ“±', 'ğ”¥', 'ğ–'],
    \ ['i', 'â±', 'áµ¢', 'ğ•š', 'ğ’¾', 'ğ“²', 'ğ”¦', 'ğ–'],
    \ ['j', 'Ê²', ' ', 'ğ•›', 'ğ’¿', 'ğ“³', 'ğ”§', 'ğ–'],
    \ ['k', 'áµ', ' ', 'ğ•œ', 'ğ“€', 'ğ“´', 'ğ”¨', 'ğ–'],
    \ ['l', 'Ë¡', ' ', 'ğ•', 'ğ“', 'ğ“µ', 'ğ”©', 'ğ–‘'],
    \ ['m', 'áµ', ' ', 'ğ•', 'ğ“‚', 'ğ“¶', 'ğ”ª', 'ğ–’'],
    \ ['n', 'â¿', ' ', 'ğ•Ÿ', 'ğ“ƒ', 'ğ“·', 'ğ”«', 'ğ–“'],
    \ ['o', 'áµ’', 'â‚’', 'ğ• ', 'â„´', 'ğ“¸', 'ğ”¬', 'ğ–”'],
    \ ['p', 'áµ–', ' ', 'ğ•¡', 'ğ“…', 'ğ“¹', 'ğ”­', 'ğ–•'],
    \ ['q', ' ', ' ', 'ğ•¢', 'ğ“†', 'ğ“º', 'ğ”®', 'ğ––'],
    \ ['r', 'Ê³', ' ', 'ğ•£', 'ğ“‡', 'ğ“»', 'ğ”¯', 'ğ–—'],
    \ ['s', 'Ë¢', ' ', 'ğ•¤', 'ğ“ˆ', 'ğ“¼', 'ğ”°', 'ğ–˜'],
    \ ['t', 'áµ—', ' ', 'ğ•¥', 'ğ“‰', 'ğ“½', 'ğ”±', 'ğ–™'],
    \ ['u', 'áµ˜', 'áµ¤', 'ğ•¦', 'ğ“Š', 'ğ“¾', 'ğ”²', 'ğ–š'],
    \ ['v', 'áµ›', ' ', 'ğ•§', 'ğ“‹', 'ğ“¿', 'ğ”³', 'ğ–›'],
    \ ['w', 'Ê·', ' ', 'ğ•¨', 'ğ“Œ', 'ğ”€', 'ğ”´', 'ğ–œ'],
    \ ['x', 'Ë£', ' ', 'ğ•©', 'ğ“', 'ğ”', 'ğ”µ', 'ğ–'],
    \ ['y', 'Ê¸', ' ', 'ğ•ª', 'ğ“', 'ğ”‚', 'ğ”¶', 'ğ–'],
    \ ['z', 'á¶»', ' ', 'ğ•«', 'ğ“', 'ğ”ƒ', 'ğ”·', 'ğ–Ÿ'],
    \ ['A', 'á´¬', ' ', 'ğ”¸', 'ğ’œ', 'ğ“', 'ğ”„', 'ğ•¬'],
    \ ['B', 'á´®', ' ', 'ğ”¹', 'â„¬', 'ğ“‘', 'ğ”…', 'ğ•­'],
    \ ['C', ' ', ' ', 'â„‚', 'ğ’', 'ğ“’', ' ', 'ğ•®'],
    \ ['D', 'á´°', ' ', 'ğ”»', 'ğ’Ÿ', 'ğ““', 'ğ”‡', 'ğ•¯'],
    \ ['E', 'á´±', ' ', 'ğ”¼', 'â„°', 'ğ“”', 'ğ”ˆ', 'ğ•°'],
    \ ['F', ' ', ' ', 'ğ”½', 'â„±', 'ğ“•', 'ğ”‰', 'ğ•±'],
    \ ['G', 'á´³', ' ', 'ğ”¾', 'ğ’¢', 'ğ“–', 'ğ”Š', 'ğ•²'],
    \ ['H', 'á´´', ' ', 'â„', 'â„‹', 'ğ“—', ' ', 'ğ•³'],
    \ ['I', 'á´µ', ' ', 'ğ•€', 'â„', 'ğ“˜', ' ', 'ğ•´'],
    \ ['J', 'á´¶', ' ', 'ğ•', 'ğ’¥', 'ğ“™', 'ğ”', 'ğ•µ'],
    \ ['K', 'á´·', ' ', 'ğ•‚', 'ğ’¦', 'ğ“š', 'ğ”', 'ğ•¶'],
    \ ['L', 'á´¸', ' ', 'ğ•ƒ', 'â„’', 'ğ“›', 'ğ”', 'ğ•·'],
    \ ['M', 'á´¹', ' ', 'ğ•„', 'â„³', 'ğ“œ', 'ğ”', 'ğ•¸'],
    \ ['N', 'á´º', ' ', 'â„•', 'ğ’©', 'ğ“', 'ğ”‘', 'ğ•¹'],
    \ ['O', 'á´¼', ' ', 'ğ•†', 'ğ’ª', 'ğ“', 'ğ”’', 'ğ•º'],
    \ ['P', 'á´¾', ' ', 'â„™', 'ğ’«', 'ğ“Ÿ', 'ğ”“', 'ğ•»'],
    \ ['Q', ' ', ' ', 'â„š', 'ğ’¬', 'ğ“ ', 'ğ””', 'ğ•¼'],
    \ ['R', 'á´¿', 'áµ£', 'â„', 'â„›', 'ğ“¡', ' ', 'ğ•½'],
    \ ['S', ' ', ' ', 'ğ•Š', 'ğ’®', 'ğ“¢', 'ğ”–', 'ğ•¾'],
    \ ['T', 'áµ€', ' ', 'ğ•‹', 'ğ’¯', 'ğ“£', 'ğ”—', 'ğ•¿'],
    \ ['U', 'áµ', ' ', 'ğ•Œ', 'ğ’°', 'ğ“¤', 'ğ”˜', 'ğ–€'],
    \ ['V', ' ', 'áµ¥', 'ğ•', 'ğ’±', 'ğ“¥', 'ğ”™', 'ğ–'],
    \ ['W', 'áµ‚', ' ', 'ğ•', 'ğ’²', 'ğ“¦', 'ğ”š', 'ğ–‚'],
    \ ['X', ' ', 'â‚“', 'ğ•', 'ğ’³', 'ğ“§', 'ğ”›', 'ğ–ƒ'],
    \ ['Y', ' ', ' ', 'ğ•', 'ğ’´', 'ğ“¨', 'ğ”œ', 'ğ–„'],
    \ ['Z', ' ', ' ', 'â„¤', 'ğ’µ', 'ğ“©', ' ', 'ğ–…']]

  if s:context_conceal =~ 'l'
    for symbol in s:contextLatinSymbols
      call s:ContextConcealScript(symbol[0], symbol[1], symbol[2])
    endfor
  endif

  " Numeric: {{{2
  let s:contextNumericSymbols = [
    \ ['0' , 'â°', 'â‚€', 'ğŸ˜'],
    \ ['1' , 'Â¹', 'â‚', 'ğŸ™'],
    \ ['2' , 'Â²', 'â‚‚', 'ğŸš'],
    \ ['3' , 'Â³', 'â‚ƒ', 'ğŸ›'],
    \ ['4' , 'â´', 'â‚„', 'ğŸœ'],
    \ ['5' , 'âµ', 'â‚…', 'ğŸ'],
    \ ['6' , 'â¶', 'â‚†', 'ğŸ'],
    \ ['7' , 'â·', 'â‚‡', 'ğŸŸ'],
    \ ['8' , 'â¸', 'â‚ˆ', 'ğŸ '],
    \ ['9' , 'â¹', 'â‚‰', 'ğŸ¡'],
    \ ['=' , 'Ë­', 'â‚Œ', ' '],
    \ ['+' , 'âº', 'â‚Š', ' '],
    \ ['-' , 'â»', 'â‚‹', ' '],
    \ ['/' , 'ËŠ', 'Ë', ' '],
    \ ['(' , 'â½', 'â‚', ' '],
    \ [')' , 'â¾', 'â‚', ' '],
    \ ['<' , 'Ë‚', 'Ë±', ' '],
    \ ['>' , 'Ëƒ', 'Ë²', ' '],
    \ ['\.', 'Ë™', 'â€¸', ' ']]

  if s:context_conceal =~ 'n'
    for symbol in s:contextNumericSymbols
      call s:ContextConcealScript(symbol[0], symbol[1], symbol[2])
    endfor
  endif

  " Blackboard And Calligraphic And Fraktur: {{{2
  " TODO more characters inside \math..{} with contains?
  if s:context_conceal =~ 'b'
    for symbol in s:contextLatinSymbols
      call s:ContextConcealSymbol(  '\\mathbb{'.symbol[0].'}', symbol[3])
      call s:ContextConcealSymbol( '\\mathcal{'.symbol[0].'}', symbol[4])
      call s:ContextConcealSymbol('\\mathfrak{'.symbol[0].'}', symbol[6])
    endfor
    for symbol in s:contextNumericSymbols
      call s:ContextConcealSymbol('\\mathbb{'.symbol[0].'}', symbol[3])
    endfor
    if s:context_conceal =~ 'B'
      for symbol in s:contextLatinSymbols
        call s:ContextConcealSymbol('\\'.symbol[0].symbol[0].'\>', symbol[3])
      endfor
    endif
  endif

  " Accents: {{{2
  " Unicode names:
  "   bar   -> macron
  "   check -> caron
  "   ddot  -> diaeresis
  "   hat   -> circumflex
  let s:contextAccentSymbols = [
    \ ['acute'    , 'Â´', 'Ì'],
    \ ['bar'      , 'Â¯', 'Ì„'],
    \ ['breve'    , 'Ë˜', 'Ì†'],
    \ ['check'    , 'Ë‡', 'ÌŒ'],
    \ ['ddot'     , 'Â¨', 'Ìˆ'],
    \ ['dot'      , 'Ë™', 'Ì‡'],
    \ ['grave'    , '`', 'Ì€'],
    \ ['hat'      , 'Ë†', 'Ì‚'],
    \ ['widehat'  , 'Ë†', 'á·'],
    \ ['tilde'    , 'Ëœ', 'Ìƒ'],
    \ ['widetilde', 'Ëœ', 'Í '],
    \ ['vec'      , 'â†’', 'âƒ—']]

  " This is a bit hacked code, but it works...
  if s:context_conceal =~ 'a'
    for symbol in s:contextAccentSymbols
      call s:ContextRegisterAccent(symbol[0], symbol[2])
      for letter in s:contextGreekSymbols
        call s:ContextConcealAccent(symbol[0], '\\'.letter[0], letter[1])
      endfor
      for letter in s:contextLatinSymbols
        call s:ContextConcealAccent(symbol[0],      letter[0], letter[0])
      endfor
      " Old solution just replaced the command:
      "call s:ContextConcealSymbol('\\'.symbol[0].'\>', symbol[1])
    endfor
  endif

endif

" Tabulation: {{{1
" -----------

if has('conceal')
  "syn match   contextTabulate   '\\\(NC\|RC\|HC\|SC\|VL\)\>'     conceal cchar=|
  "syn match   contextTabulate   '\\\(EQ\|RQ\|HQ\|SQ\|TQ\)\>'     conceal cchar=:
  "syn match   contextTabulate   '\\\(NR\|FR\|MR\|LR\|AR\|SR\)\>' conceal cchar=+
  "syn match   contextTabulate   '\\\(NB\|TB\)\>'                 conceal cchar=Â±
  "syn match   contextTabulate   '\\\(HL\|FL\|ML\|LL\)\>'         conceal cchar=-
  syn match   contextTabulate   '\\\uC\>' conceal cchar=| " Columns
  syn match   contextTabulate   '\\\uN\>' conceal cchar=| " Number columns
  syn match   contextTabulate   '\\\uQ\>' conceal cchar=: " Equality columns
  syn match   contextTabulate   '\\\uR\>' conceal cchar=+ " Rows
  syn match   contextTabulate   '\\\uB\>' conceal cchar=Â± " Blocks and Blanks
  syn match   contextTabulate   '\\\uL\>' conceal cchar=- " Lines
endif

" Typing And Coding: {{{1
" ------------------

syn region  contextTyping     display matchgroup=contextDelimiter start='@'                                end='@'
syn region  contextTyping     display matchgroup=contextDelimiter start='\\type\s*\z(\A\)'                 end='\z1'
syn region  contextTyping     display matchgroup=contextDelimiter start='\\\%(type\?\|tex\|arg\|mat\)\s*{' end='}'

"FIXME arguments after \starttyping
syn region  contextTyping             matchgroup=contextBlock   start='\\start\z(\a*\)typing\>'       end='\\stop\z1typing\>' contains=contextComment
syn region  contextTyping             matchgroup=contextBlock   start='\\start\z(\u\+\)\>'            end='\\stop\z1\>'       contains=contextComment

fun! s:ContextIncludeSyntax(name, startstop)
  exe 'syn include @'.a:name.'Top syntax/'.a:name.'.vim'
  unlet b:current_syntax
  exe 'syn region context'.a:name.'Code transparent matchgroup=contextBlock start="\\start\z('.a:startstop.'\)\>" end="\\stop\z1\>" contains=@'.a:name.'Top'
endfun

let s:contextCodeNames = [
  \ ['ruby'   , 'RUBY'   ],
  \ ['haskell', 'HASKELL'],
  \ ['lua'    , '\(luacode\|LUA\)'],
  \ ['mp'     , '\(reusable\|static\|unique\|use\)\?MP\(environment\|figure\|graphic\|inclusions\|instance\)\?']]

for name in s:contextCodeNames
  call s:ContextIncludeSyntax(name[0], name[1])
endfor

" Comments: {{{1
" ---------

syn keyword contextTodo       TODO FIXME NOTE XXX contained

" The match group is there to prevent spell checking of the %D %M and %C themselves.
syn region  contextComment    display oneline         start='\\\@!%'    end='$' contains=contextTodo
syn match   contextComment    display '^%[CDM]\s'
"syn region  contextComment    display oneline matchgroup=contextComment start='^%[CDM]\s' end='$' contains=TOP,contextComment

syn region  contextHiding     matchgroup=contextBlock start='\\starthiding\>' end='\\stophiding\>'

" Folding: {{{1
" --------

if exists('g:context_no_fold')
 let s:context_fold = 0
else
 let s:context_fold = 1
endif

"FIXME Folding of heads inside comments.
if has('folding') && s:context_fold == 1
  " All highlighting is done elsewhere. Here we just match the boundaries of a fold and use transparent.
  setlocal fdm=syntax

  " We don't fold document structure but only use them as containers for sections.
  " The keepend is necessary to get the first match. This isn't a problem, because there can't be a \chapter inside a \chapter etcetera.
  syn region  contextDocument   transparent keepend      start='\\starttext\>'                end='\\stoptext\>'
  syn region  contextDocument   transparent keepend      start='\\startcomponent\>'           end='\\stopcomponent\>'
  syn region  contextDocument   transparent keepend      start='\\startproduct\>'             end='\\stopproduct\>'
  syn region  contextDocument   transparent keepend      start='\\startproject\>'             end='\\stopproject\>'
  syn region  contextDocument   transparent keepend      start='\\startenvironment\>'         end='\\stopenvironment\>'

  syn region  contextHiding     transparent keepend fold start='\\starthiding\>'              end='\\stophiding\>'                                                                                                             contained containedin=contextDocument
  "syn region  contextHiding     transparent keepend fold start='\\startstandardmakeup\>'      end='\\stopstandardmakeup\>'                                                                                                     contained containedin=contextDocument

  " Fold everything up to subsections.
  syn region  contextPart       transparent keepend fold start='\\startpart\>'                end='\\stoppart\>'                                                                                                               contained containedin=contextDocument
  syn region  contextPart       transparent keepend fold start='\\part\>'                     end='\ze\\\%(part\|stop\%(text\|component\|product\|project\|environment\)\)\>'                                                  contained containedin=contextDocument

  syn region  contextChapter    transparent keepend fold start='\\startchapter\>'             end='\\stopchapter\>'                                                                                                            contained containedin=contextDocument,contextPart
  syn region  contextChapter    transparent keepend fold start='\\starttitle\>'               end='\\stoptitle\>'                                                                                                              contained containedin=contextDocument,contextPart
  syn region  contextChapter    transparent keepend fold start='\\\%(chapter\|title\)\>'      end='\ze\\\%(chapter\|title\|part\|stop\%(text\|component\|product\|project\|environment\)\)\>'                                  contained containedin=contextDocument,contextPart

  syn region  contextSection    transparent keepend fold start='\\startsection\>'             end='\\stopsection\>'                                                                                                            contained containedin=contextDocument,contextChapter
  syn region  contextSection    transparent keepend fold start='\\startsubject\>'             end='\\stopsubject\>'                                                                                                            contained containedin=contextDocument,contextChapter
  syn region  contextSection    transparent keepend fold start='\\\%(section\|subject\)\>'    end='\ze\\\%(section\|subject\|chapter\|title\|part\|stop\%(text\|component\|product\|project\|environment\)\)\>'                contained containedin=contextDocument,contextChapter

  syn region  contextSubsection transparent keepend fold start='\\startsubsection\>'          end='\\stopsubsection\>'                                                                                                         contained containedin=contextDocument,contextSection
  syn region  contextSubsection transparent keepend fold start='\\startsubsubject\>'          end='\\stopsubsubject\>'                                                                                                         contained containedin=contextDocument,contextSection
  syn region  contextSubsection transparent keepend fold start='\\sub\%(section\|subject\)\>' end='\ze\\\%(\%(sub\)\?\%(section\|subject\)\|chapter\|title\|part\|stop\%(text\|component\|product\|project\|environment\)\)\>' contained containedin=contextDocument,contextSection
endif

" Syncing: {{{1
" ========

" Syncing from the start is simply the best way to go.
syn sync    fromstart
" Syncing against comments is a nice taught, but won't work with comments inside math and protected environments.
"syn sync    ccomment          contextComment
" Maybe some day we'll sync against sections...

" Highlight Definitions: {{{1
" ======================

" Commands:
hi def link contextCommand        Function

hi def link contextBlock          Statement
hi def link contextCondition      Conditional
hi def link contextLoop           Repeat
hi def link contextHead           Keyword

hi def link contextStructure      Include
hi def link contextStructureError Error

" Definitions And Setups:
hi def link contextDefine         Define
hi def link contextSetup          contextDefine

" Groups And Arguments:
hi def link contextDelimiter      Delimiter
hi def link contextMismatch       Error

hi def link contextNumber         Number
hi def link contextDimension      contextNumber
hi def link contextLabel          Tag
hi def link contextConstant       Constant

" Fonts And Styles:
hi def link contextFont           Type
hi def link contextStyle          contextFont

hi def      contextNormal         gui=NONE
hi def      contextItalic         gui=italic
hi def link contextSlanted        contextItalic
hi def      contextBold           gui=bold
hi def      contextBoldItalic     gui=bold,italic
hi def link contextBoldSlanted    contextBoldItalic

hi def link contextAlert          contextBold
hi def link contextOuterEmph      contextItalic
hi def link contextInnerEmph      contextNormal

" Specials:
hi def link contextEscaped        contextSpecial
hi def link contextSpecial        Special
hi def link contextScriptError    Error
hi def link contextParameter      Identifier
hi def link contextParameterError Error

" Math:
hi def link contextMath           String
hi def link contextMathText       Normal
hi def link contextMathSymbol     SpecialChar
hi def link contextMathAccent     contextMathSymbol
hi def link contextSuperscript    contextMathSymbol
hi def link contextSubscript      contextMathSymbol
hi!    link Conceal               contextMathSymbol

" Tabulates:
hi def link contextTabulate       SpecialChar

" Typing:
hi def link contextTyping         String

" Comments:
hi def link contextTodo           Todo
hi def link contextComment        Comment
hi def link contextHiding         contextComment

" Finalize Syntaxfile: {{{1
" ====================

let b:current_syntax = 'context'

let &cpo = s:cpo_save
unlet s:cpo_save

" vim: nowrap fdm=marker spell spl=en
